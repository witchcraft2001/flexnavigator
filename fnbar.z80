
;[]-----------------------------------------------------------[]
InitScrBar
	LD	C,(IX+BarPAGEl)
	LD	B,(IX+BarPAGEh)	;Кол-во	элем.на	стр.
	LD	L,C
	LD	H,B
	LD	E,(IX+BarMAXl)
	LD	D,(IX+BarMAXh)	;макс.элементов
	LD	A,D
	OR	E
	JR	NZ,$+3
	INC	DE
	OR	A
	SBC	HL,DE
	JR	C,$+4
	ADD	HL,DE
	EX	DE,HL
	PUSH	DE
	LD	E,(IX+BarMaxLl)
	LD	D,(IX+BarMaxLh)	;Кол-во	вид.элем.*размер бара
	CALL	Mult16X16	;DE*BC=HL+DE
	POP	BC		;/макс.элементов
	CALL	Divis32X16	;HL+DE/BC=DE (HL-ост)
	LD	A,H
	OR	L
	JR	Z,$+3
	INC	DE
	LD	HL,#0003
	SBC	HL,DE
	JR	C,$+5
	LD	DE,#0003
	LD	(IX+BarLENl),E	;=длина	бара
	LD	(IX+BarLENh),D	;=длина	бара
	RET 
PrnHScrBar
	PUSH	IY
	IN	A,(YPORT)
	PUSH	AF
	IN	A,(PAGE1)
	LD	LY,A
	LD	L,(IX+BarFRSTl)	;FirstFile
	LD	H,(IX+BarFRSTh)
	LD	E,(IX+BarPAGEl)
	LD	D,(IX+BarPAGEh)
	LD	C,(IX+BarMAXl)	;EquipFiles
	LD	B,(IX+BarMAXh)
	LD	A,B
	OR	C
	JR	NZ,$+3
	INC	BC
	PUSH	BC
	PUSH	HL
	ADD	HL,DE
	SBC	HL,BC
	POP	HL
	JR	C,PrHScr1
	JR	Z,PrHScr1
	LD	L,C
	LD	H,B
	SBC	HL,DE
	JR	NC,PrHScr1
	LD	HL,#0000
PrHScr1	EX	DE,HL
	LD	C,(IX+BarMaxLl)
	LD	B,(IX+BarMaxLh)
	CALL	Mult16X16
	POP	BC
	CALL	Divis32X16
	LD	A,D
	OR	E
	JR	Z,PrHBar1
	PUSH	DE
	SRL	D
	RR	E
	JR	NC,$+3
	INC	DE
	LD	L,(IX+BarXl)
	LD	H,(IX+BarXh)
	SRL	H
	RR	L
	SET	6,H
	LD	A,(IX+BarYl)
	LD	C,A
	LD	B,#88
	LD	A,#50
	OUT	(PAGE1),A
	DI 
	LD	D,D
	LD	A,#08
	LD	B,B
FillHl1	LD	A,C
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),B
	LD	B,B
	INC	HL
	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,FillHl1
	LD	A,LY
	OUT	(PAGE1),A
	EI 
FillHle	POP	DE
PrHBar1	PUSH	DE
	LD	L,(IX+BarXl)
	LD	H,(IX+BarXh)
	ADD	HL,DE
	LD	E,(IX+BarLEFTl)
	LD	D,(IX+BarLEFTh)
	LD	A,D
	OR	E
	JR	Z,PrHBar2
	EX	DE,HL
	LD	A,#C0
	OUT	(YPORT),A
	LD	(HL),E
	INC	HL
	LD	(HL),D
	EX	DE,HL
PrHBar2	PUSH	HL
	SRL	H
	RR	L
	SET	6,H
	LD	A,(IX+BarYl)
	LD	C,A
	LD	E,(IX+BarLENl)
	LD	D,(IX+BarLENh)
	LD	A,D
	OR	A
	JR	NZ,PrHBr21
	LD	A,E
	CP	#06
	JR	NC,PrHBr21
	LD	E,#06
PrHBr21	SRL	D
	RR	E
	JR	C,$+3
	DEC	DE
	DEC	DE
	LD	A,#50
	OUT	(PAGE1),A
	DI 
	LD	D,D
	LD	A,#06
	LD	B,B
	LD	B,#FF
	LD	A,C
	OUT	(YPORT),A
	LD	(HL),B
	INC	A
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),B
	LD	B,B
	LD	(HL),#F7
	INC	HL
HBarLp1	LD	A,C
	OUT	(YPORT),A
	LD	(HL),B
	INC	A
	OUT	(YPORT),A
	LD	A,#88
	LD	E,E
	LD	(HL),A
	LD	B,B
	LD	(HL),#77
	INC	HL
	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,HBarLp1
	LD	A,C
	OUT	(YPORT),A
	LD	(HL),#F7
	INC	A
	OUT	(YPORT),A
	LD	A,#77
	LD	E,E
	LD	(HL),A
	LD	B,B
	LD	(HL),A
	INC	HL
	EX	DE,HL
	LD	A,LY
	OUT	(PAGE1),A
	EI 
	POP	HL
	LD	C,(IX+BarLENl)
	LD	B,(IX+BarLENh)
	PUSH	BC
	ADD	HL,BC
	LD	C,(IX+BarRGHTl)
	LD	B,(IX+BarRGHTh)
	LD	A,B
	OR	C
	JR	Z,PrHBar3
	LD	A,#C0
	OUT	(YPORT),A
	LD	A,L
	LD	(BC),A
	INC	BC
	LD	A,H
	LD	(BC),A
PrHBar3	POP	BC
	LD	L,(IX+BarMaxLl)
	LD	H,(IX+BarMaxLh)
	OR	A
	SBC	HL,BC
	POP	BC
	JR	Z,HBarExt
	JR	C,HBarExt
	OR	A
	SBC	HL,BC
	JR	Z,HBarExt
	JR	C,HBarExt
	EX	DE,HL
	SRL	D
	RR	E
	JR	Z,HBarExt
	LD	A,(IX+BarYl)
	LD	C,A
	LD	B,#88
	LD	A,#50
	OUT	(PAGE1),A
	DI 
	LD	D,D
	LD	A,#08
	LD	B,B
FillHl2	LD	A,C
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),B
	LD	B,B
	INC	HL
	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,FillHl2
	LD	A,LY
	OUT	(PAGE1),A
	EI 
HBarExt	POP	AF
	OUT	(YPORT),A
	POP	IY
	RET 
PrnVScrBar
	PUSH	IY
	IN	A,(YPORT)
	PUSH	AF
	IN	A,(PAGE1)
	LD	HY,A
	LD	L,(IX+BarFRSTl)	;FirstFile
	LD	H,(IX+BarFRSTh)
	LD	E,(IX+BarPAGEl)
	LD	D,(IX+BarPAGEh)
	LD	C,(IX+BarMAXl)	;EquipFiles
	LD	B,(IX+BarMAXh)
	LD	A,B
	OR	C
	JR	NZ,$+3
	INC	BC
	PUSH	BC
	PUSH	HL
	ADD	HL,DE
	SBC	HL,BC
	POP	HL
	JR	C,PrVScr1
	JR	Z,PrVScr1
	LD	L,C
	LD	H,B
	SBC	HL,DE
	JR	NC,PrVScr1
	LD	HL,#0000
PrVScr1	EX	DE,HL
	LD	C,(IX+BarMaxLl)
	LD	B,(IX+BarMaxLh)
	CALL	Mult16X16
	POP	BC
	CALL	Divis32X16
	LD	A,(IX+BarYl)
	LD	LY,A
	LD	A,D
	OR	E
	JR	Z,PrVBar1
	PUSH	DE
	LD	A,E
	LD	(VBFill1+1),A
	LD	L,(IX+BarXl)
	LD	H,(IX+BarXh)
	SRL	H
	RR	L
	SET	6,H
	LD	D,#88
	LD	A,#50
	OUT	(PAGE1),A
	DI 
	LD	D,D
VBFill1	LD	A,#00
	LD	B,B
	LD	B,#08
FillVl1	LD	A,LY
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),D
	LD	B,B
	INC	HL
	DJNZ	FillVl1
	LD	A,LY
	ADD	A,E
	LD	LY,A
	LD	A,HY
	OUT	(PAGE1),A
	EI 
	POP	DE
PrVBar1	PUSH	DE
	LD	L,(IX+BarYl)
	LD	H,(IX+BarYh)
	ADD	HL,DE
	LD	E,(IX+BarLEFTl)
	LD	D,(IX+BarLEFTh)
	LD	A,D
	OR	E
	JR	Z,PrVBar2
	EX	DE,HL
	LD	A,#C0
	OUT	(YPORT),A
	LD	(HL),E
	INC	HL
	LD	(HL),D
	EX	DE,HL
PrVBar2	PUSH	HL
	LD	L,(IX+BarXl)
	LD	H,(IX+BarXh)
	SRL	H
	RR	L
	SET	6,H
	LD	A,(IX+BarLENl)
	LD	C,A
	SUB	#02
	LD	(VBarLen+1),A
	LD	A,#50
	OUT	(PAGE1),A
	DI 
	LD	D,D
VBarLen	LD	A,#00
	LD	B,B
	LD	DE,#FF88
	LD	A,LY
	OUT	(YPORT),A
	LD	(HL),D
	INC	A
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),D
	LD	B,B
	LD	(HL),#F7
	INC	HL
	LD	B,#06
VBarLp1	LD	A,LY
	OUT	(YPORT),A
	LD	(HL),D
	INC	A
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),E
	LD	B,B
	LD	(HL),#77
	INC	HL
	DJNZ	VBarLp1
	LD	A,LY
	OUT	(YPORT),A
	LD	(HL),#F7
	INC	A
	OUT	(YPORT),A
	LD	A,#77
	LD	E,E
	LD	(HL),A
	LD	B,B
	LD	(HL),A
	INC	HL
	LD	A,LY
	ADD	A,C
	LD	LY,A
	LD	A,HY
	OUT	(PAGE1),A
	EI 
	POP	HL
	LD	C,(IX+BarLENl)
	LD	B,(IX+BarLENh)
	PUSH	BC
	ADD	HL,BC
	LD	C,(IX+BarRGHTl)
	LD	B,(IX+BarRGHTh)
	LD	A,B
	OR	C
	JR	Z,PrVBar3
	LD	A,#C0
	OUT	(YPORT),A
	LD	A,L
	LD	(BC),A
	INC	BC
	LD	A,H
	LD	(BC),A
PrVBar3	POP	BC
	LD	L,(IX+BarMaxLl)
	LD	H,(IX+BarMaxLh)
	OR	A
	SBC	HL,BC
	POP	BC
	JR	Z,VBarExt
	JR	C,VBarExt
	OR	A
	SBC	HL,BC
	JR	Z,VBarExt
	JR	C,VBarExt
	LD	A,L
	LD	(VBFill2+1),A
	LD	L,(IX+BarXl)
	LD	H,(IX+BarXh)
	SRL	H
	RR	L
	SET	6,H
	LD	D,#88
	LD	A,#50
	OUT	(PAGE1),A
	DI 
	LD	D,D
VBFill2	LD	A,#00
	LD	B,B
	LD	B,#08
FillVl2	LD	A,LY
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),D
	LD	B,B
	INC	HL
	DJNZ	FillVl2
	LD	A,HY
	OUT	(PAGE1),A
	EI 
VBarExt	POP	AF
	OUT	(YPORT),A
	POP	IY
	RET 
;[]-----------------------------------------------------------[]
InitProcess
	SUB	A
	LD	(IX+ProcCURl1),A
	LD	(IX+ProcCURh1),A
	LD	(IX+ProcCURl2),A
	LD	(IX+ProcCURh2),A
	LD	(IX+ProcCORRl),A
	LD	(IX+ProcCORRh),A
	LD	C,(IX+ProcMaxLl)	;Макс/макс.длину процеса
	LD	B,(IX+ProcMaxLh)
	LD	E,(IX+ProcMAXl1)
	LD	D,(IX+ProcMAXh1)
	LD	L,(IX+ProcMAXl2)
	LD	H,(IX+ProcMAXh2)
	CALL	Divis32X16		;HL+DE/BC=DE (HL-ост)
	LD	A,H
	OR	L
	JR	Z,$+3
	INC	DE			;= коэффициент
	LD	HL,9
	OR	A
	SBC	HL,DE
	JR	C,InitPrE
	LD	A,(IX+ProcMAXl2)
	OR	(IX+ProcMAXh2)
	JR	NZ,InitPrE
	LD	E,(IX+ProcMAXl1)
	LD	D,(IX+ProcMAXh1)
	LD	BC,1000
	LD	(IX+ProcCORRl),C
	LD	(IX+ProcCORRh),B
	CALL	Mult16X16
	LD	C,(IX+ProcMaxLl)	;Макс/макс.длину процеса
	LD	B,(IX+ProcMaxLh)
	CALL	Divis32X16		;HL+DE/BC=DE (HL-ост)
InitPrE	LD	(IX+ProcCOEFl),E
	LD	(IX+ProcCOEFh),D
	RET 
PrnProcess
	PUSH	IY
	IN	A,(YPORT)
	PUSH	AF
	IN	A,(PAGE1)
	LD	LY,A
	LD	E,(IX+ProcCURl1)
	LD	D,(IX+ProcCURh1)
	LD	L,(IX+ProcCURl2)
	LD	H,(IX+ProcCURh2)
	LD	A,(IX+ProcCORRl)
	OR	(IX+ProcCORRh)
	JR	Z,PrProc0
	LD	C,(IX+ProcCORRl)
	LD	B,(IX+ProcCORRh)
	CALL	Mult16X16
PrProc0	LD	C,(IX+ProcCOEFl)
	LD	B,(IX+ProcCOEFh)
	CALL	Divis32X16
	LD	L,(IX+ProcXl)
	LD	H,(IX+ProcXh)
	SRL	H
	RR	L
	SET	6,H
	LD	A,D
	OR	E
	JR	Z,PrProc1
	PUSH	DE
	PUSH	HL
	LD	L,(IX+ProcMaxLl)
	LD	H,(IX+ProcMaxLh)
	OR	A
	SBC	HL,DE
	JR	NC,$+4
	ADD	HL,DE
	EX	DE,HL
	POP	HL
	SRL	D
	RR	E
	JR	NC,$+3
	INC	DE
	LD	A,(IX+ProcYl)
	LD	C,A
	LD	A,(IX+ProcCOLOR)
	AND	#F0
	LD	B,A
	RRCA 
	RRCA 
	RRCA 
	RRCA 
	OR	B
	LD	B,A
	LD	A,#50
	OUT	(PAGE1),A
	DI 
	LD	D,D
	LD	A,#0A
	LD	B,B
FillPr1	LD	A,C
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),B
	LD	B,B
	INC	HL
	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,FillPr1
	LD	A,LY
	OUT	(PAGE1),A
	EI 
	POP	DE
PrProc1	PUSH	HL
	LD	L,(IX+ProcMaxLl)
	LD	H,(IX+ProcMaxLh)
	OR	A
	SBC	HL,DE
	EX	DE,HL
	POP	HL
	JR	Z,PrProcE
	JR	C,PrProcE
	SRL	D
	RR	E
	JR	NC,$+3
	INC	DE
	LD	A,(IX+ProcYl)
	LD	C,A
	LD	A,(IX+ProcCOLOR)
	AND	#0F
	LD	B,A
	RLCA 
	RLCA 
	RLCA 
	RLCA 
	OR	B
	LD	B,A
	LD	A,#50
	OUT	(PAGE1),A
	DI 
	LD	D,D
	LD	A,#0A
	LD	B,B
FillPr2	LD	A,C
	OUT	(YPORT),A
	LD	E,E
	LD	(HL),B
	LD	B,B
	INC	HL
	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,FillPr2
	LD	A,LY
	OUT	(PAGE1),A
	EI 
PrProcE	POP	AF
	OUT	(YPORT),A
	POP	IY
	RET 


