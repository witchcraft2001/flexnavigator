
;[]---------------------------------------------------------[]
PrnASCIIpage
	LD	HL,PrintVProc
	PUSH	HL
	LD	HL,(FileShift)
	LD	(TempShift),HL
	LD	HL,(FileShift+2)
	LD	(TempShift+2),HL
	LD	B,#1C
PrnASCIIlp
	PUSH	BC
	CALL	PutASCIIline   ;Выводим	строку в буф и на экран
	POP	BC
	JR	C,FillASCIIdn  ;CY-Закончились байты для печати
	LD	A,B
	ADD	A,#08
	LD	B,A
	CP	#E4
	JR	NZ,PrnASCIIlp
	LD	HL,(TempShift)
	LD	(NextShift),HL
	LD	HL,(TempShift+2)
	LD	(NextShift+2),HL
	SUB	A
	LD	(EmptyLines),A
	RET 
;Процедура отчистки нижней области экрана
FillASCIIdn
	LD	A,#E4
	SUB	B
	LD	(FLenASC),A
	JR	Z,FASCIIe	;Z-нет отчистки
	LD	A,B
	LD	(FPosASC),A
	CALL	MakeWindow
	DEFB	#80
	DEFB	#02	;Head Place
	DEFW	#0000
FPosASC	DEFW	#0000
	DEFW	#0280
FLenASC	DEFW	#0000
	DEFB	PanelCol
	DEFB	#00
FASCIIe	LD	HL,(TempShift)
	LD	(NextShift),HL
	LD	HL,(TempShift+2)
	LD	(NextShift+2),HL
	LD	A,(FLenASC)
	SRL	A
	SRL	A
	SRL	A
	LD	(EmptyLines),A
	RET 
;Процедура печати тек.строки в буфер и на экран
PutASCIIline
	LD	HX,B
	LD	A,(ASCIImode)
	OR	A
	LD	C,#50
	JR	Z,$+4
	LD	C,#F0
	PUSH	BC
	LD	HL,(FileLenght)
	LD	BC,(TempShift)
	OR	A
	SBC	HL,BC
	EX	DE,HL
	LD	HL,(FileLenght+2)
	LD	BC,(TempShift+2)
	SBC	HL,BC
	POP	BC
	RET	C
	LD	B,#F0
	JR	NZ,PutLine
	LD	A,D
	OR	A
	JR	NZ,PutLine
	OR	E
	SCF 
	RET	Z
	LD	B,E

PutLine	LD	HL,LineBuffer
	IN	A,(PAGE1)
	PUSH	AF
	CALL	InitFPages
PutLnLoop
	CALL	TestFPages
	LD	A,(DE)
	INC	DE
	CP	#0D
	JR	NZ,PutLine1
	CALL	TestFPages
	LD	A,(DE)
	CP	#0A
	JR	NZ,PutLnExit
	INC	DE
	JR	PutLnExit
PutLine1
	CP	#0A
	JR	Z,PutLnExit
	CP	#09
	JR	NZ,PutLine2
PutTab	LD	(HL),#20
	INC	L
	LD	A,L
	CP	C
	JR	Z,PutLine3
	AND	#07
	JR	NZ,PutTab
	JR	PutLine3
PutLine2
	CALL	DecodeSymbol
	LD	(HL),A
	LD	A,L
	CP	C
	JR	Z,PutLine3
	INC	L
PutLine3
	DJNZ	PutLnLoop

	CALL	TestFPages
	LD	A,(DE)
	CP	#0D
	JR	NZ,PutLnExit
	INC	DE
	CALL	TestFPages
	LD	A,(DE)
	CP	#0A
	JR	NZ,PutLnExit
	INC	DE
PutLnExit
	POP	AF
	OUT	(PAGE1),A
	CALL	GetFPages
	LD	A,(ASCIImode)
	OR	A
	JR	NZ,PutLineUWR
	LD	A,#50
	SUB	L
	JR	Z,PutLine4
	LD	B,A
	LD	A,#20
	LD	(HL),A
	INC	L
	DJNZ	$-2
PutLine4
	LD	HL,LineBuffer
	LD	DE,#0000
	LD	B,#50
	LD	C,HX
	LD	A,PanelCol*#10+FileCol
	CALL	PrnTxtLnNormC
	OR	A
	RET 
DecodeSymbol
	CP	#80
	RET	C	
	PUSH	HL
	SUB	#80
	LD	HL,Table866
.table	EQU	$-2
	ADD	A,L
	LD	L,A
	JR	NC,.nc
	INC	H
.nc	LD	A,(HL)
	POP	HL
	RET

PutLineUWR
	LD	C,HX
	LD	A,L
	LD	(SaveUW+1),A
	LD	A,(ASCIIaddX)
	ADD	A,#50
	SUB	L
	JR	Z,PutLnUW1
	JR	C,PutLnUW1
	LD	B,A
	LD	A,#20
	LD	(HL),A
	INC	L
	DJNZ	$-2
PutLnUW1
	LD	E,#00
	LD	B,#50
	LD	H,high LineBuffer
	LD	A,(ASCIIaddX)
	LD	L,A		;смещение от начала строки
	OR	A
	JR	Z,PutLnUW2
	PUSH	HL
	PUSH	BC
	LD	HL,SpaceLeft
	LD	D,#00
	LD	B,#01
	LD	A,PanelCol*#10+#03	;(Yellow)
	CALL	PrnTxtLnNormC
	POP	BC
	POP	HL
	INC	L
	DEC	B
	LD	A,(ASCIIaddX)
	LD	E,#08
PutLnUW2
	LD	D,A
SaveUW	LD	A,#00
	SUB	D
	JR	C,PutLnUW3
	CP	#51
	JR	C,PutLnUW3
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	HL,SpaceRight
	LD	DE,#0278
	LD	B,#01
	LD	A,PanelCol*#10+#03	;(Yellow)
	CALL	PrnTxtLnNormC
	POP	BC
	POP	DE
	POP	HL
	DEC	B
PutLnUW3
	LD	D,#00
	LD	A,PanelCol*#10+FileCol	;(Yellow)
	CALL	PrnTxtLnNormC
	OR	A
	RET 
SpaceLeft
	DEFB	#1B
SpaceRight
	DEFB	#1A
;[]==========================================================[]
ChCodePage
	LD	HL,ViewerBegin
	EX	(SP),HL
        LD      A,(CodePage)
        INC     A
        CP      3
        JR      C,.no
        XOR     A
.no     LD      (CodePage), A
	RET
;[]==========================================================[]
ChASCIImode
	LD	HL,ViewerBegin
	EX	(SP),HL
	LD	A,(ASCIImode)
	XOR	#01
	LD	(ASCIImode),A
	RET	Z
	SUB	A
	LD	(ASCIIaddX),A
	RET 
;[]----------------------------------------------------------[]
ScrollLeft
	LD	A,(ASCIImode)
	OR	A
	RET	Z
	LD	A,(ASCIIaddX)
	OR	A
	RET	Z
	DEC	A
	LD	(ASCIIaddX),A
	CALL	PrnASCIIpage
	RET 
;[]----------------------------------------------------------[]
ScrollRght
	LD	A,(ASCIImode)
	OR	A
	RET	Z
	LD	A,(ASCIIaddX)
	CP	#A0
	RET	Z
	INC	A
	LD	(ASCIIaddX),A
	CALL	PrnASCIIpage
	RET 
;[]----------------------------------------------------------[]
THome
	LD	A,(ASCIImode)
	OR	A
	RET	Z
	LD	A,(ASCIIaddX)
	OR	A
	RET	Z
	SUB	A
	LD	(ASCIIaddX),A
	CALL	PrnASCIIpage
	RET 
;[]----------------------------------------------------------[]
TEnd
	LD	A,(ASCIImode)
	OR	A
	RET	Z
	LD	A,(ASCIIaddX)
	CP	#A0
	RET	Z
	LD	A,#A0
	LD	(ASCIIaddX),A
	CALL	PrnASCIIpage
	RET 
;[]----------------------------------------------------------[]
TLineUpMs
	CALL	TLineUp
	RET	C
	LD	B,#0A
TUpMs1	PUSH	BC
	LD	C,#03
	RST	#30
	POP	BC
	BIT	0,A
	RET	Z
	HALT 
	DJNZ	TUpMs1
TUpMs2	CALL	TLineUp
	RET	C
	LD	C,#03
	RST	#30
	BIT	0,A
	RET	Z
	JR	TUpMs2
TLineUp
	LD	HL,(FileShift)
	LD	DE,(FileShift+2)
	LD	A,H
	OR	L
	OR	D
	OR	E
	SCF 
	RET	Z
	CALL	SearchUp
	LD	(FileShift),HL
	LD	(FileShift+2),DE
	LD	(TempShift),HL
	LD	(TempShift+2),DE
	LD	HL,EmptyLines
	LD	A,(HL)
	DEC	(HL)
	OR	A
	JR	NZ,TLineU1
	INC	(HL)
	LD	HL,(NextShift)
	LD	DE,(NextShift+2)
	CALL	SearchUp
	LD	(NextShift),HL
	LD	(NextShift+2),DE
TLineU1	CALL	ScrollSCRdown
	LD	B,#1C
	CALL	PutASCIIline	    ;Создаем и печатаем	строку
	CALL	PrintVProc
	OR	A
	RET 
;[]----------------------------------------------------------[]
TLineDwnMs
	CALL	TLineDwn
	RET	C
	LD	B,#0A
TDnMs1	PUSH	BC
	LD	C,#03
	RST	#30
	POP	BC
	BIT	0,A
	RET	Z
	HALT 
	DJNZ	TDnMs1
TDnMs2	CALL	TLineDwn
	RET	C
	LD	C,#03
	RST	#30
	BIT	0,A
	RET	Z
	JR	TDnMs2
TLineDwn
	LD	HL,(NextShift)
	LD	DE,(NextShift+2)
	LD	BC,(FileLenght)
	OR	A
	SBC	HL,BC
	LD	BC,(FileLenght+2)
	EX	DE,HL
	SBC	HL,BC
	CCF 
	RET	C
	LD	HL,(FileShift)
	LD	DE,(FileShift+2)
	CALL	SearchDown
	LD	(FileShift),HL
	LD	(FileShift+2),DE
	LD	HL,(NextShift)
	LD	DE,(NextShift+2)
	LD	(TempShift),HL
	LD	(TempShift+2),DE
	CALL	ScrollSCRup
	LD	B,#DC
	CALL	PutASCIIline	    ;Создаем и печатаем	строку
	LD	HL,(TempShift)
	LD	DE,(TempShift+2)
	LD	(NextShift),HL
	LD	(NextShift+2),DE
	CALL	PrintVProc
	OR	A
	RET 
;[]----------------------------------------------------------[]
TPageUpMs
	CALL	TPageUp
	RET	C
	LD	B,#0A
TUpMs1p	PUSH	BC
	LD	C,#03
	RST	#30
	POP	BC
	BIT	0,A
	RET	Z
	HALT 
	DJNZ	TUpMs1p
TUpMs2p	CALL	TPageUp
	RET	C
	LD	C,#03
	RST	#30
	BIT	0,A
	RET	Z
	JR	TUpMs2p
TPageUp
	LD	HL,(FileShift)
	LD	DE,(FileShift+2)
	LD	A,H
	OR	L
	OR	D
	OR	E
	SCF 
	RET	Z
	LD	B,#19
TPageU1	PUSH	BC
	CALL	SearchUp
	POP	BC
	JR	C,TPageU2
	DJNZ	TPageU1
TPageU2	LD	(FileShift),HL
	LD	(FileShift+2),DE
	CALL	PrnASCIIpage
	OR	A
	RET 
;[]----------------------------------------------------------[]
TPageDwnMs
	CALL	TPageDwn
	RET	C
	LD	B,#0A
TDnMs1p	PUSH	BC
	LD	C,#03
	RST	#30
	POP	BC
	BIT	0,A
	RET	Z
	HALT 
	DJNZ	TDnMs1p
TDnMs2p	CALL	TPageDwn
	RET	C
	LD	C,#03
	RST	#30
	BIT	0,A
	RET	Z
	JR	TDnMs2p
TPageDwn
	LD	HL,(NextShift)
	LD	DE,(NextShift+2)
	LD	BC,(FileLenght)
	OR	A
	SBC	HL,BC
	LD	BC,(FileLenght+2)
	EX	DE,HL
	SBC	HL,BC
	CCF 
	RET	C
	LD	HL,(NextShift)
	LD	(FileShift),HL
	LD	HL,(NextShift+2)
	LD	(FileShift+2),HL
	CALL	PrnASCIIpage
	OR	A
	RET 
;[]----------------------------------------------------------[]
TBegFile
	LD	HL,ResFire
	PUSH	HL
	LD	HL,(FileShift)
	LD	DE,(FileShift+2)
	LD	A,H
	OR	D
	OR	L
	OR	E
	RET	Z
	LD	HL,#0000
	LD	(FileShift),HL
	LD	(FileShift+2),HL
	CALL	PrnASCIIpage
	RET 
;[]----------------------------------------------------------[]
TEndFile
	LD	HL,ResFire
	PUSH	HL
	LD	HL,(NextShift)
	LD	DE,(NextShift+2)
	LD	BC,(FileLenght)
	OR	A
	SBC	HL,BC
	LD	BC,(FileLenght+2)
	EX	DE,HL
	SBC	HL,BC
	RET	NC
	LD	HL,(FileLenght)
	LD	DE,(FileLenght+2)
	LD	B,#19
TEndFl1	PUSH	BC
	CALL	SearchUp
	POP	BC
	RET	C
	DJNZ	TEndFl1
	LD	(FileShift),HL
	LD	(FileShift+2),DE
	CALL	PrnASCIIpage
	RET 
;[]==========================================================[]
SearchUp
	LD	B,#F0
	LD	A,D
	OR	E
	JR	NZ,SearchUpBeg
	LD	A,H
	OR	A
	JR	NZ,SearchUpBeg
	OR	L
	SCF 
	RET	Z
	LD	B,L
SearchUpBeg
	IN	A,(PAGE1)
	PUSH	AF
	PUSH	HL
	PUSH	DE
	LD	A,H		;Выделяем номер	страницы
	RLA 
	RL	E
	RLA 
	RL	E
	LD	D,high PagesList
	LD	A,(DE)		;Тек.страница
	OUT	(PAGE1),A
	RES	7,H
	SET	6,H
	LD	C,#00
	LD	LX,C

	INC	C
	DEC	B
	DEC	HL
	BIT	6,H
	JR	NZ,SrchUp0
	DEC	E
	LD	A,(DE)
	OUT	(PAGE1),A
	SET	6,H
SrchUp0	LD	A,(HL)
	CP	#0A
	JR	NZ,SrchUp1+1
	LD	A,B
	OR	A
	JR	Z,SrchUpEx
	INC	C
	DEC	B
	DEC	HL
	BIT	6,H
	JR	NZ,SrchUp1
	DEC	E
	LD	A,(DE)
	OUT	(PAGE1),A
	SET	6,H
SrchUp1	LD	A,(HL)
	CP	#0D
	JR	Z,SearchUpLp
	DEC	C
	INC	B
	INC	HL
	BIT	7,H
	JR	Z,SearchUpLp1
	INC	E
	LD	A,(DE)
	OUT	(PAGE1),A
	RES	7,H
	SET	6,H
	JR	SearchUpLp1
SearchUpLp
	LD	A,B
	OR	A
	JR	Z,SrchUpEx
SearchUpLp1
	DEC	HL
	BIT	6,H
	JR	NZ,SrchUp2
	DEC	E
	LD	A,(DE)
	OUT	(PAGE1),A
	SET	6,H
SrchUp2	LD	A,(HL)
	CP	#0A
	JR	Z,SrchUpEx
	CP	#0D
	JR	Z,SrchUpEx
	INC	LX
	INC	C
	DJNZ	SearchUpLp1
SrchUpEx
	POP	DE
	POP	HL
	LD	B,#00
	OR	A
	SBC	HL,BC
	LD	C,B
	EX	DE,HL
	SBC	HL,BC
	EX	DE,HL
	POP	AF
	OUT	(PAGE1),A
	LD	A,(ASCIImode)
	OR	A
	RET	NZ
	LD	A,LX
	CP	#51
	CCF 
	RET	NC
	LD	C,#50
SrchUpEx0
	ADD	HL,BC
	JR	NC,$+3
	INC	DE
	SUB	#50
	CP	#51
	JR	NC,SrchUpEx0
	OR	A
	RET 
;[]----------------------------------------------------------[]
SearchDown
	LD	(SrchHL+1),HL
	LD	(SrchDE+1),DE
	LD	A,(ASCIImode)
	OR	A
	LD	B,#50
	JR	Z,$+4
	LD	B,#F0
	PUSH	BC
	LD	C,L
	LD	B,H
	LD	HL,(FileLenght)
	OR	A
	SBC	HL,BC
	LD	C,E
	LD	B,D
	EX	DE,HL
	LD	HL,(FileLenght+2)
	SBC	HL,BC
	POP	BC
	RET	C
	JR	NZ,SearchDnBeg
	LD	A,D
	OR	A
	JR	NZ,SearchDnBeg
	OR	E
	SCF 
	RET	Z
	LD	B,E
SearchDnBeg
SrchHL	LD	HL,#0000
SrchDE	LD	DE,#0000
	IN	A,(PAGE1)
	PUSH	AF
	PUSH	HL
	PUSH	DE
	LD	A,H		;Выделяем номер	страницы
	RLA 
	RL	E
	RLA 
	RL	E
	LD	D,high PagesList
	LD	A,(DE)		;Тек.страница
	OUT	(PAGE1),A
	RES	7,H
	SET	6,H
	LD	C,#00
	DEC	HL
SearchDnLp
	INC	C
	INC	HL
	BIT	7,H
	JR	Z,SrchDn0
	INC	E
	LD	A,(DE)
	OUT	(PAGE1),A
	RES	7,H
	SET	6,H
SrchDn0	LD	A,(HL)
	CP	#0D
	JR	NZ,SrchE1
	INC	HL
	BIT	7,H
	JR	Z,SrchDn1
	INC	E
	LD	A,(DE)
	OUT	(PAGE1),A
	RES	7,H
	SET	6,H
SrchDn1	LD	A,(HL)
	CP	#0A
	JR	NZ,SrchEEx
	INC	C
	JR	SrchEEx

SrchE1	CP	#0A
	JR	Z,SrchEEx
	DJNZ	SearchDnLp

	INC	HL
	BIT	7,H
	JR	Z,SrchDnE
	INC	E
	LD	A,(DE)
	OUT	(PAGE1),A
	RES	7,H
	SET	6,H
SrchDnE	LD	A,(HL)
	CP	#0D
	JR	NZ,SrchEEx
	INC	C
	INC	HL
	BIT	7,H
	JR	Z,SrchDE1
	INC	E
	LD	A,(DE)
	OUT	(PAGE1),A
	RES	7,H
	SET	6,H
SrchDE1	LD	A,(HL)
	CP	#0A
	JR	NZ,SrchEEx
	INC	C
SrchEEx	POP	DE
	POP	HL
	LD	B,#00
	ADD	HL,BC
	LD	C,B
	EX	DE,HL
	ADC	HL,BC
	EX	DE,HL
	POP	AF
	OUT	(PAGE1),A
	OR	A
	RET 

Table866
	db	3Fh,3Fh,27h,3Fh,22h,3Ah,0C5h,0D8h
	db	3Fh,25h,3Fh,3Ch,3Fh,3Fh,3Fh,3Fh
	db	3Fh,27h,27h,22h,22h,07h,2Dh,2Dh
	db	3Fh,54h,3Fh,3Eh,3Fh,3Fh,3Fh,3Fh
	db	0FFh,0F6h,0F7h,3Fh,0FDh,3Fh,0B3h,15h
	db	0F0h,63h,0F2h,3Ch,0BFh,2Dh,52h,0F4h
	db	0F8h,2Bh,49h,69h,3Fh,0E7h,14h,0FAh
	db	0F1h,0FCh,0F3h,3Eh,3Fh,3Fh,3Fh,0F5h
	db	80h,81h,82h,83h,84h,85h,86h,87h
	db	88h,89h,8Ah,8Bh,8Ch,8Dh,8Eh,8Fh
	db	90h,91h,92h,93h,94h,95h,96h,97h
	db	98h,99h,9Ah,9Bh,9Ch,9Dh,9Eh,9Fh
	db	0A0h,0A1h,0A2h,0A3h,0A4h,0A5h,0A6h,0A7h
	db	0A8h,0A9h,0AAh,0ABh,0ACh,0ADh,0AEh,0AFh
	db	0E0h,0E1h,0E2h,0E3h,0E4h,0E5h,0E6h,0E7h
	db	0E8h,0E9h,0EAh,0EBh,0ECh,0EDh,0EEh,0EFh
	; koi8-r
TableKoi
	db	0C4h,0B3h,0DAh,0BFh,0C0h,0D9h,0C3h,0B4h
	db	0C2h,0C1h,0C5h,0DFh,0DCh,0DBh,0DDh,0DEh
	db	0B0h,0B1h,0B2h,0F4h,0FEh,0F9h,0FBh,0F7h
	db	0F3h,0F2h,0FFh,0F5h,0F8h,0FDh,0FAh,0F6h
	db	0CDh,0BAh,0D5h,0F1h,0D6h,0C9h,0B8h,0B7h
	db	0BBh,0D4h,0D3h,0C8h,0BEh,0BDh,0BCh,0C6h
	db	0C7h,0CCh,0B5h,0F0h,0B6h,0B9h,0D1h,0D2h
	db	0CBh,0CFh,0D0h,0CAh,0D8h,0D7h,0CEh,0FCh
	db	0EEh,0A0h,0A1h,0E6h,0A4h,0A5h,0E4h,0A3h
	db	0E5h,0A8h,0A9h,0AAh,0ABh,0ACh,0ADh,0AEh
	db	0AFh,0EFh,0E0h,0E1h,0E2h,0E3h,0A6h,0A2h
	db	0ECh,0EBh,0A7h,0E8h,0EDh,0E9h,0E7h,0EAh
	db	9Eh,80h,81h,96h,84h,85h,94h,83h
	db	95h,88h,89h,8Ah,8Bh,8Ch,8Dh,8Eh
	db	8Fh,9Fh,90h,91h,92h,93h,86h,82h
	db	9Ch,9Bh,87h,98h,9Dh,99h,97h,9Ah
