FECSign	DB "FEC"
HomeKeyFlag
	DB	0
IndentFlag
	DB	0
BracketsFlag
	DB	0
ExitDlgFlag
	DB	0

;[]==========================================================[]
ReadEditorIni
	ld	a,0
.iniReaded	equ	$-1
	and	a
	ret	nz
	inc	a
	ld	(.iniReaded),a
	LD	C,#02
	PUSH	IY
	RST	#10
	POP	IY
	LD	HL,WorkBuffer
	ADD	A,"A"
	LD	(HL),A
	INC	HL
	LD	(HL),":"
	INC	HL
	LD	C,Dss.CurDir
	PUSH	IY
	RST	#10
	POP	IY
	IN	A,(PAGE1)
	PUSH	AF
	LD	A,(FunctPg)
	OUT	(PAGE1),A
	LD	HL,FlexDIRECTORY
	LD	C,Dss.ChDir
	PUSH	IY
	RST	#10
	POP	IY
	EX	AF,AF'
	POP	AF
	OUT	(PAGE1),A
	EX	AF,AF'
	JP	C,.exit
	LD	HL,SettingsFileName
	ld	c,Dss.Open
        ld	a,1
	PUSH	IY
        rst	10h
	pop	iy
        jr	c,.exit
        ld	(.file),a	
        ld	hl,LineBuffer
	push	hl
	ld	de,LineBuffer+1
	ld	(hl),0
	ld	bc,7
	ldir
	pop	hl
        ld	de,7
        ld	c,Dss.Read
	PUSH	IY
        rst	10h
	pop	iy
        jr	c,.close
        ld	hl,(LineBuffer)
        ld	de,"F" + "E" * 256
        and	a
        sbc	hl,de
        jr	nz,.close
        ld	hl,LineBuffer+2
        ld	a,(hl)
        cp	"C"
        jr	nz,.close
	inc	hl
	ld	a,(hl)
	ld	(THomeString.param),a
	inc	hl
	ld	a,(hl)
	ld	(CalcStartTabs.param),a
	inc	hl
	ld	a,(hl)
	ld	(AutoBrackets),a
	inc	hl
	ld	a,(hl)
	ld	(QuitAsk.param),a

.close	ld	a,0
.file	equ	$-1
	ld	c,Dss.Close
	PUSH	IY
	rst	#10
	pop	IY
.exit	LD	HL,WorkBuffer
	LD	C,#1D
	PUSH	IY
	RST	#10
	POP	IY
	ret
;[]==========================================================[]
EditorSetup
	CALL	PutString
	CALL	ResCursor

	LD	A,(QuitAsk.param)
	ld	(ExitDlgFlag),a
	CALL	ParamToSymbol
	LD	(ExitDlgFlg),A

	LD	A,(THomeString.param)
	ld	(HomeKeyFlag),a
	CALL	ParamToSymbol
	LD	(HomeKeyFlg),A

	LD	A,(CalcStartTabs.param)
	ld	(IndentFlag),a
	CALL	ParamToSymbol
	LD	(IndentFlg),A

	LD	A,(AutoBrackets)
	ld	(BracketsFlag),a
	CALL	ParamToSymbol
	LD	(BracketsFlg),A

	CALL	MakeWindow
	DEFB	#01
	DEFW	#00C8,#0050
	DEFW	#00F0,#006E
	DEFB	FW_RECT	;Head place
	DEFW	#0004,#0002
	DEFW	#00E8,#000A
	DEFB	#00
	DEFB	FW_LABEL	;Text Line
	DEFW	#0054,#0003
	DEFB	#0F
	DEFB	"Editor settings",0

	DEFB	FW_RECT_DEEP	;Углубл
	DEFW	#0010,#0013
	DEFW	#0014,#000B
	DEFB	FW_RECT	        ;Head place
	DEFW	#0012,#0014
	DEFW	#0010,#0009
	DEFB	#0F
	DEFB	FW_LABEL	;Text Line
	DEFW	#0014,#0015
	DEFB	#F0
ExitDlgFlg
	DEFB	#FF,#00
	DEFB	FW_LABEL	;Text Line
	DEFW	#0028,#0016
	DEFB	#80
	DEFB	"Confirmation of closing the editor",0

	DEFB	FW_RECT_DEEP	;Углубл
	DEFW	#0010,#0023
	DEFW	#0014,#000B
	DEFB	FW_RECT	        ;Head place
	DEFW	#0012,#0024
	DEFW	#0010,#0009
	DEFB	#0F
	DEFB	FW_LABEL	;Text Line
	DEFW	#0014,#0025
	DEFB	#F0
HomeKeyFlg
	DEFB	#FF,#00
	DEFB	FW_LABEL	;Text Line
	DEFW	#0028,#0026
	DEFB	#80
	DEFB	"find first symbol on Home key",0

	DEFB	FW_RECT_DEEP	;Углубл
	DEFW	#0010,#0033
	DEFW	#0014,#000B
	DEFB	FW_RECT	        ;Head place
	DEFW	#0012,#0034
	DEFW	#0010,#0009
	DEFB	#0F
	DEFB	FW_LABEL	;Text Line
	DEFW	#0014,#0035
	DEFB	#F0
IndentFlg
	DEFB	#FF,#00
	DEFB	FW_LABEL	;Text Line
	DEFW	#0028,#0036
	DEFB	#80
	DEFB	"auto Indent",0

	DEFB	FW_RECT_DEEP	;Углубл
	DEFW	#0010,#0043
	DEFW	#0014,#000B
	DEFB	FW_RECT	        ;Head place
	DEFW	#0012,#0044
	DEFW	#0010,#0009
	DEFB	#0F
	DEFB	FW_LABEL	;Text Line
	DEFW	#0014,#0045
	DEFB	#F0
BracketsFlg
	DEFB	#FF,#00
	DEFB	FW_LABEL	;Text Line
	DEFW	#0028,#0046
	DEFB	#80
	DEFB	"auto Brackets",0

	DEFB	FW_H_LINE	;Horizontal line
	DEFW	#0004,#0054
	DEFW	#00E8
	DEFB	FW_RECT_CONVEX	;UpRectan
	DEFW	#004D,#005A
	DEFW	#0048,#000E
	DEFB	FW_LABEL	;Text Line
	DEFW	#0060,#005D
	DEFB	#80
	DEFB	"Cancel",0
	DEFB	FW_RECT_CONVEX	;UpRectan
	DEFW	#00A0,#005A
	DEFW	#0040,#000E
	DEFB	FW_LABEL	;Text Line
	DEFW	#00BC,#005D
	DEFB	#80
	DEFB	"Ok",0
	DEFB	FW_END
SetupLoop
	LD	IX,SetupTab
	CALL	TestCoords
	LD	C,#31
	RST	#10
	JR	Z,SetupLoop
	LD	A,E
	RES	5,A
	CP	#1B
	JP	Z,SetupCancel
	CP	#0D
	JP	Z,SetupOkey
	; CP	"C"
	; JP	Z,SetupCancel
	CP	"O"
	JP	Z,SetupOkey
	ld	hl,SetupLoop
	push	hl
	CP	"B"
	JP	Z,SetupAutoBracketsInvert
	CP	"C"
	JP	Z,SetupConfirmDlgInvert
	CP	"H"
	JP	Z,SetupHomeKeyInvert
	CP	"I"
	JP	Z,SetupAutoIndentInvert
        ret

;A - parameter
;Out - A = A == 0 ? " " : "v"
ParamToSymbol
	OR	A
	LD	A,#FE
	RET	Z
	INC	A
	RET

CheckTxt
	DEFB	#00,#00

SetupConfirmDlgInvert
	LD	HL,CheckTxt
	LD	A,(ExitDlgFlag)
	XOR	#01
	LD	(ExitDlgFlag),A
	call	ParamToSymbol
	LD	(HL),A
	LD	DE,#00DC
	LD	BC,#0065
	LD	A,#F0
	CALL	PrnTxtLnIFF
	CALL	ResFire
	RET 

SetupHomeKeyInvert
	LD	HL,CheckTxt
	LD	A,(HomeKeyFlag)
	XOR	#01
	LD	(HomeKeyFlag),A
	call	ParamToSymbol
	LD	(HL),A
	LD	DE,#00DC
	LD	BC,#0075
	LD	A,#F0
	CALL	PrnTxtLnIFF
	CALL	ResFire
	RET 

SetupAutoIndentInvert
	LD	HL,CheckTxt
	LD	A,(IndentFlag)
	XOR	#01
	LD	(IndentFlag),A
	call	ParamToSymbol
	LD	(HL),A
	LD	DE,#00DC
	LD	BC,#0085
	LD	A,#F0
	CALL	PrnTxtLnIFF
	CALL	ResFire
	RET 

SetupAutoBracketsInvert
	LD	HL,CheckTxt
	LD	A,(BracketsFlag)
	XOR	#01
	LD	(BracketsFlag),A
	call	ParamToSymbol
	LD	(HL),A
	LD	DE,#00DC
	LD	BC,#0095
	LD	A,#F0
	CALL	PrnTxtLnIFF
	CALL	ResFire
	RET 

SetupOkey
	ld	a,(ExitDlgFlag)
	LD	(QuitAsk.param),a

	LD	A,(HomeKeyFlag)
	LD	(THomeString.param),A

	ld	A,(IndentFlag)
	LD	(CalcStartTabs.param),A

	ld	a,(BracketsFlag)
	LD	(AutoBrackets),a

	LD	C,#02			;Write ini-file
	PUSH	IY
	RST	#10
	LD	HL,WorkBuffer
	ADD	A,"A"
	LD	(HL),A
	INC	HL
	LD	(HL),":"
	INC	HL
	LD	C,Dss.CurDir
	RST	#10
	IN	A,(PAGE1)
	PUSH	AF
	LD	A,(FunctPg)
	OUT	(PAGE1),A
	LD	HL,FlexDIRECTORY
	LD	C,Dss.ChDir
	RST	#10
	EX	AF,AF'
	POP	AF
	OUT	(PAGE1),A
	EX	AF,AF'
	JP	C,.exit
	LD	HL,SettingsFileName
	ld	c,Dss.Create
        ld	a,FileAttrib.Arch
        rst	10h
        jr	c,.exit
        ld	(.file),a	
        jr	c,.close
	ld	hl,FECSign
	ld	de,7
        ld	c,Dss.Write
        rst	10h
.close	ld	a,0
.file	equ	$-1
	ld	c,Dss.Close
	rst	#10
.exit	LD	HL,WorkBuffer
	LD	C,Dss.ChDir
	RST	#10
	POP	IY
SetupCancel
	CALL	MakeWindow
	DEFB	#7F
	DEFB	#00
	RET
;[]==========================================================[]
;Формат	таблицы
;	+00  2	Xmin
;	+02  2	Xmax
;	+04  2	Ymin
;	+06  2	Ymax
;	+08  2	0 - простой об'ект
;		бит 0 -	кнопка
;		бит 1 -	рабочая	кнопка
;		бит 2 -	инверт 1
;		бит 3 -	статичный инверт
;		бит 4 -	программа запускается без адреса возвр.
;			и без огня
;		бит 5 -	программа запускается без адреса возвр.
;			по левой кнопке	огня
;		бит 6 -	программа запускается без адреса возвр.
;			по правой кнопке огня
;		бит 7 -	вызвать	функцию	закрытия окна перед
;			запуском программы без огня
;		бит 8 -	вызвать	функцию	закрытия окна перед
;			запуском программы по левой кнопке
;		бит 9 -	вызвать	функцию	закрытия окна перед
;			запуском программы по правой кнопке
;		бит15 -	это переключатель (инверт при огне не
;			убирать)
;	+0A  2	Адрес обработчика при не нажатом огне
;	+0C  2	Адрес обработчика при нажатом огне (лев)
;	+0E  2	Адрес обработчика при нажатом огне (прав)

;[]==========================================================[]
SetupTab
;      DEFW #01C8,#0210,#00A2,#00B0,#0001,#0000,SaveSetup,#0000
     DEFW #0114,#015C,#00AA,#00B8,#0021,#0000,SetupCancel,#0000
     DEFW #0168,#01A8,#00AA,#00B8,#0021,#0000,SetupOkey,#0000

     DEFW #00D8,#0192,#0063,#006D,#0000,#0000,SetupConfirmDlgInvert,#0000
     DEFW #00D8,#0186,#0073,#007D,#0000,#0000,SetupHomeKeyInvert,#0000
     DEFW #00D8,#0129,#0083,#008D,#0000,#0000,SetupAutoIndentInvert,#0000
     DEFW #00D8,#0132,#0093,#009D,#0000,#0000,SetupAutoBracketsInvert,#0000

     DEFW #0000,#00C8,#0000,#0100,#0020,#0000,SetupCancel,#0000
     DEFW #01B8,#0280,#0000,#0100,#0020,#0000,SetupCancel,#0000
     DEFW #0000,#0280,#0000,#0050,#0020,#0000,SetupCancel,#0000
     DEFW #0000,#0280,#00BC,#0100,#0020,#0000,SetupCancel,#0000

     DEFW #8000