
;[]----------------------------------------------------------[]
PrintDisks
	LD	L,(IY+PanelXl)
	LD	H,(IY+PanelXh)
	INC	HL
	INC	HL
	LD	(DiskX1),HL
	LD	DE,#0004
	ADD	HL,DE
	LD	(DiskX2),HL
	LD	DE,#0006
	ADD	HL,DE
	LD	(NameX),HL
	LD	A,"A"
	LD	(DISK),A
	PUSH	IY
	POP	HL
	LD	DE,BegPath
	ADD	HL,DE
	LD	A,(HL)
	SUB	"A"
	LD	C,A
	LD	A,(EquipDisks)
	LD	B,A
PutDsLp	LD	A,C
	OR	A
	LD	DE,#0773
	JR	Z,$+5
	LD	DE,#0880
	LD	A,D
	LD	(ColRect),A
	LD	A,E
	LD	(ColName),A
	CALL	MakeWindow
	DEFB	#80
	DEFB	#04
DiskX1	DEFW	#0000	;Xpos
	DEFW	#001E	;Ypos
LnDiskX	DEFW	#001C	;lenX
	DEFW	#000C	;lenY
	DEFB	#02
DiskX2	DEFW	#0000	;Xpos
	DEFW	#0020	;Ypos
	DEFW	#0014	;lenX
	DEFW	#0008	;lenY
ColRect	DEFB	#00
	DEFB	#01
NameX	DEFW	#0000
	DEFW	#0020
ColName	DEFB	#00
DISK	DEFB	" ",0
	DEFB	#00
	LD	HL,(DiskX1)
	LD	DE,(LnDiskX)
	ADD	HL,DE
	LD	(DiskX1),HL
	LD	HL,(DiskX2)
	ADD	HL,DE
	LD	(DiskX2),HL
	LD	HL,(NameX)
	ADD	HL,DE
	LD	(NameX),HL
	LD	HL,DISK
	INC	(HL)
	DEC	C
	DJNZ	PutDsLp
	RET 
;[]----------------------------------------------------------[]
PrintPath
	BIT	7,(IY+PanelFlg)
	LD	A,#00
	LD	C,#0F
	JR	NZ,$+6
	LD	A,#07
	LD	C,#7F
	LD	(HeadCol),A
	LD	A,C
	LD	(PathCol+1),A
	LD	L,(IY+PanelXl)
	LD	H,(IY+PanelXh)
	LD	(PanelHX),HL
	CALL	MakeWindow
	DEFB	#80
	DEFB	#02
PanelHX	DEFW	#0000,#002E
	DEFW	#0136,#000C
HeadCol	DEFB	#00
	DEFB	#00
	PUSH	IY
	POP	HL
	LD	BC,BegPath
	ADD	HL,BC
	PUSH	HL
	LD	DE,WorkBuffer
	SUB	A
	LDI 
	CP	(HL)
	JR	NZ,$-3
	DEC	HL
	LD	A,"\\"
	CP	(HL)
	JR	Z,$+4
	LD	(DE),A
	INC	DE
	POP	HL
	SUB	A
	LD	(DE),A
	BIT	1,(IY+ViewFlg)
	JR	NZ,PrPath1
	LD	BC,#000D
	SBC	HL,BC
	BIT	5,(IY+SortFlg)
	JR	NZ,$+5
	LD	HL,NORMmask
	LDIR 
PrPath1	LD	IX,#012C
	LD	HL,(PanelHX)
	LD	DE,WorkBuffer
	LD	BC,#0006
	ADD	HL,BC
	EX	DE,HL
	LD	C,#30
PathCol	LD	A,#00
	CALL	PrnRangPathL
	RET 
;[]----------------------------------------------------------[]
PrintDOSPath
	PUSH	IY
	POP	HL
	LD	BC,BegPath
	ADD	HL,BC
	LD	DE,WorkBuffer
	SUB	A
	LDI 
	CP	(HL)
	JR	NZ,$-3
	DEC	HL
	LD	A,"\\"
	CP	(HL)
	JR	Z,$+4
	LD	(DE),A
	INC	DE
	LD	A,">"
	LD	(DE),A
	INC	DE
	SUB	A
	LD	(DE),A
	CALL	MakeWindow
	DEFB	#80
	DEFB	#02
	DEFW	#0004,#00E7
	DEFW	#00F6,#0008
	DEFB	#08
	DEFB	#00
	LD	IX,#00F6
	LD	HL,WorkBuffer
	LD	DE,#00FA
	LD	BC,#00E7
	LD	A,#80
	CALL	PrnRangPathR
	RET 
;[]----------------------------------------------------------[]
PrnFileInfo
	BIT	1,(IY+ViewFlg)
	JP	NZ,PrnTreeInfo		;Tree
	LD	A,(IY+PanelMFl)
	OR	(IY+PanelMFh)
	JP	NZ,PrnSelInfo
	IN	A,(PAGE1)
	PUSH	AF
	BIT	0,(IY+PanelFlg)
	LD	A,(DIRpage1)
	JR	Z,$+5
	LD	A,(DIRpage2)
	OUT	(PAGE1),A
	LD	L,(IY+PanelFFl)
	LD	H,(IY+PanelFFh)
	LD	E,(IY+PanelCFl)
	LD	D,(IY+PanelCFh)
	ADD	HL,DE
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	E,L
	LD	D,H
	ADD	HL,HL
	ADD	HL,DE
	SET	6,H
	LD	DE,WorkBuffer
	LD	BC,#0018
	CALL	MoveBytes
	POP	AF
	OUT	(PAGE1),A
	LD	HL,WorkBuffer+#18
	LD	B,38
	LD	A," "
	LD	(HL),A
	INC	HL
	DJNZ	$-2
	LD	(HL),#00
	LD	IX,WorkBuffer
	LD	HL,WorkBuffer+1
	LD	A,(HL)
	OR	A
	JR	Z,PrntFI
	LD	DE,WorkBuffer+#18
	LD	B,#08
EFI0	LD	A,(HL)
	CP	#20
	JR	Z,EFI1
	INC	HL
	LD	(DE),A
	INC	DE
	DJNZ	EFI0
EFI1	LD	A,B
	OR	A
	JR	Z,$+5
	INC	HL
	DJNZ	$-1
	LD	B,#03
	INC	HL
	LD	A,(HL)
	CP	#20
	JR	NZ,EFI3
EFI2	LD	A,(HL)
	INC	HL
	LD	(DE),A
	INC	DE
	DJNZ	EFI2
EFI3	LD	A,B
	OR	A
	JR	Z,EndFI
	LD	A,"."
	LD	(DE),A
	INC	DE
EFI4	LD	A,(HL)
	INC	HL
	LD	(DE),A
	INC	DE
	DJNZ	EFI4
EndFI	LD	DE,WorkBuffer+#18+#0D
	CALL	GetFileLen
	LD	DE,WorkBuffer+#18+#18
	CALL	GetData
	INC	DE
	CALL	GetTime
PrntFI	LD	HL,WorkBuffer+#18
	LD	E,(IY+PanelXl)
	LD	D,(IY+PanelXh)
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	LD	BC,#00D7
	LD	A,#80
	CALL	PrnTxtLnNorm
	RET 
GetFileLen
	BIT	4,(IX+#0E)
	JP	NZ,PDirectory
	RES	7,(IX+#0E)
	LD	L,(IX+#0F)
	LD	H,(IX+#10)
	EXX 
	LD	L,(IX+#11)
	LD	H,(IX+#12)
	EXX 
	LD	BC,#CA00
	EXX 
	LD	BC,#3B9A	;1'000'000'000
	EXX 
	CALL	GetLenN32
	LD	BC,#E100
	EXX 
	LD	BC,#05F5	;100'000'000
	EXX 
	CALL	GetLenN32
	LD	BC,#9680
	EXX 
	LD	BC,#0098	;10'000'000
	EXX 
	CALL	GetLenN32
	LD	BC,#4240
	EXX 
	LD	BC,#000F	;1'000'000
	EXX 
	CALL	GetLenN32
	LD	BC,#86A0
	EXX 
	LD	BC,#0001	;100'000
	EXX 
	CALL	GetLenN32
	LD	BC,10000	;10'000
	EXX 
	LD	BC,#0000	;100'000
	EXX 
	CALL	GetLenN32
	LD	BC,1000		;1'000
	CALL	GetLenN16
	LD	BC,100		;100
	CALL	GetLenN16
	LD	BC,10		;10
	CALL	GetLenN16
	LD	A,L
	ADD	A,"0"
	LD	(DE),A
	INC	DE
	RET 
GetLenN32
	LD	A,#2F
	OR	A
GetLn32	INC	A
	SBC	HL,BC
	EXX 
	SBC	HL,BC
	EXX 
	JR	NC,GetLn32
	ADD	HL,BC
	EXX 
	ADC	HL,BC
	EXX 
	CP	#30
	JR	Z,$+6
	SET	7,(IX+#0E)
	BIT	7,(IX+#0E)
	JR	NZ,$+4
	LD	A,#20
	LD	(DE),A
	INC	DE
	RET 
GetLenN16
	LD	A,#2F
	OR	A
	INC	A
	SBC	HL,BC
	JR	NC,$-3
	ADD	HL,BC
	CP	#30
	JR	Z,$+6
	SET	7,(IX+#0E)
	BIT	7,(IX+#0E)
	JR	NZ,$+4
	LD	A,#20
	LD	(DE),A
	INC	DE
	RET 
PDirectory
	LD	HL,DirText
	LD	BC,#000A
	LDIR 
	RET 
DirText
	DEFB	"   <DIR>  "
GetData
;Дата
	EX	DE,HL
	LD	A,(IX+#13)
	CALL	Get8Bit
	LD	(HL),"."
	INC	HL
	LD	A,(IX+#14)
	CALL	Get8Bit
	LD	(HL),"."
	INC	HL
	LD	A,(IX+#15)	;Год
	CALL	Get8Bit
	EX	DE,HL
	RET 
GetTime
;Время
	EX	DE,HL
	LD	A,(IX+#16)	;Часы
	CALL	Get8Bit
	LD	(HL),":"
	INC	HL
	LD	A,(IX+#17)
	CALL	Get8Bit
	EX	DE,HL
	RET 
Get8Bit
	LD	B,#2F
	INC	B
	SUB	10
	JR	NC,$-3
	ADD	A,10
	LD	(HL),B
	INC	HL
	ADD	A,"0"
	LD	(HL),A
	INC	HL
	RET 
;
PrnTreeInfo
	CALL	MakeTreePath
	EX	DE,HL
	PUSH	IY
	POP	HL
	LD	BC,BegPath
	ADD	HL,BC
	INC	HL
	INC	HL
	EX	DE,HL
	SUB	A
	LDI 
	CP	(HL)
	JR	NZ,$-3
	LD	(DE),A
	CALL	PrintPath
	BIT	7,(IY+PanelFlg)
	CALL	NZ,PrintDOSPath
	RET 
PrnSelInfo
	LD	L,(IY+PanelMBl1)
	LD	H,(IY+PanelMBh1)
	LD	C,(IY+PanelMBl2)
	LD	B,(IY+PanelMBh2)
	LD	DE,WorkBuffer
	CALL	ConvNumTxt32
	LD	HL,CrSizeT
	LD	BC,#000A
	LDIR 
	LD	L,(IY+PanelMFl)
	LD	H,(IY+PanelMFh)
	CALL	ConvNumTxt16
	LD	HL,CrSelT
	LD	BC,#000F
	LDIR 
	LD	A,E
	LD	DE,WorkBuffer+#100
	LD	C,A
	SUB	38
	NEG 
	SRL	A
	LD	B,A
	JR	Z,PrSelI1
	LD	A,#20
	LD	(DE),A
	INC	E
	DJNZ	$-2
PrSelI1	LD	HL,WorkBuffer
	LDIR 
	LD	A,38
	SUB	E
	JR	Z,PrSelI2
	LD	B,A
	LD	A,#20
	LD	(DE),A
	INC	E
	DJNZ	$-2
PrSelI2	SUB	A
	LD	(DE),A
	LD	HL,WorkBuffer+#100
	LD	E,(IY+PanelXl)
	LD	D,(IY+PanelXh)
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	LD	BC,#00D7
	LD	A,#80
	CALL	PrnTxtLnNorm
	RET 
CrSizeT
	DEFB	" bytes in "
CrSelT
	DEFB	" selected files"
;[]----------------------------------------------------------[]
PrintDIR
	LD	A,(IY+PanelEFl)
	OR	(IY+PanelEFh)
	JR	NZ,PrnDIRECT
	LD	L,(IY+PanelXl)
	LD	H,(IY+PanelXh)
	INC	HL
	INC	HL
	LD	(ClrDIR1),HL
	LD	HL,#0134
	LD	DE,#008D
	LD	A,(IY+ViewFlg)
	AND	#03
	JR	Z,$+8
	LD	HL,#0124
	LD	DE,#0095
	LD	(ClrDIRx),HL
	LD	(ClrDIRy),DE
	CALL	MakeWindow
	DEFB	#80
	DEFB	#02
ClrDIR1	DEFW	#0000,#003C
ClrDIRx	DEFW	#0000
ClrDIRy	DEFW	#0000
	DEFB	PanelCol
	DEFB	#00
	RET 
PrnDIRECT
	BIT	0,(IY+ViewFlg)
	JP	NZ,PrintDIR1
	BIT	1,(IY+ViewFlg)
	JP	NZ,PrintDIR2
;Краткий список
	PUSH	IY
	IN	A,(PAGE1)
	PUSH	AF
	BIT	0,(IY+PanelFlg)
	LD	A,(DIRpage1)
	JR	Z,$+5
	LD	A,(DIRpage2)
	OUT	(PAGE1),A
	LD	L,(IY+PanelFFl)
	LD	H,(IY+PanelFFh)
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	E,L
	LD	D,H
	ADD	HL,HL
	ADD	HL,DE
	SET	6,H
	LD	DE,WorkBuffer
	LD	BC,#33*#18
	CALL	MoveBytes
	POP	AF
	OUT	(PAGE1),A
	LD	B,PanelCol*#10+FileCol
	LD	C,PanelCol*#10+SFileCol
	CALL	InsPrnColor
	IN	A,(YPORT)	;Сохраняем Y порт
	LD	C,A
	IN	A,(PAGE1)
	LD	B,A
	PUSH	BC
	LD	A,#50
	OUT	(PAGE1),A
	LD	HL,WorkBuffer
	PUSH	HL
	BIT	0,(IY+PanelFlg)
	LD	HL,PositTAB1
	JR	Z,$+5
	LD	HL,PositTAB2
	LD	B,#33
PrnDIR1	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	EX	(SP),HL
	PUSH	HL
	PUSH	BC
	LD	B,#0C
	CALL	PrintName
	POP	BC
	POP	HL
	LD	DE,#0019
	ADD	HL,DE
	LD	A,(HL)
	DEC	HL
	OR	A
	JR	Z,PrnDIR2
	EX	(SP),HL
	DJNZ	PrnDIR1
	JR	PrnDIRe
PrnDIR2	DEC	B
	JR	Z,PrnDIRe
	LD	HL,EmptyFile
	EX	(SP),HL
PrnDIR3	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	EX	(SP),HL
	PUSH	HL
	PUSH	BC
	LD	B,#0C
	CALL	PrintName
	POP	BC
	POP	HL
	EX	(SP),HL
	DJNZ	PrnDIR3
PrnDIRe	POP	HL
	POP	BC
	LD	A,B
	OUT	(PAGE1),A
	LD	A,C
	OUT	(YPORT),A
	POP	IY
	RET 
EmptyFile		
	DEFB	#00,"        ",#01,"   ",#00
;Подробный список
PrintDIR1
	PUSH	IY
	IN	A,(PAGE1)
	PUSH	AF
	BIT	0,(IY+PanelFlg)
	LD	A,(DIRpage1)
	JR	Z,$+5
	LD	A,(DIRpage2)
	OUT	(PAGE1),A
	LD	L,(IY+PanelFFl)
	LD	H,(IY+PanelFFh)
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	E,L
	LD	D,H
	ADD	HL,HL
	ADD	HL,DE
	SET	6,H
	LD	DE,WorkBuffer
	LD	BC,#12*#18
	CALL	MoveBytes
	POP	AF
	OUT	(PAGE1),A
	LD	B,PanelCol*#10+FileCol
	LD	C,PanelCol*#10+SFileCol
	CALL	InsPrnColor
	IN	A,(YPORT)	;Сохраняем Y порт
	LD	C,A
	IN	A,(PAGE1)
	LD	B,A
	PUSH	BC
	LD	A,#50
	OUT	(PAGE1),A
	LD	HL,WorkBuffer
	BIT	0,(IY+PanelFlg)
	LD	DE,#0008
	JR	Z,$+5
	LD	DE,#0148
	LD	BC,#123F
PrnDIRp	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	HL
	LD	DE,WorkBuffer+#200
	LD	BC,#000D
	LDIR 
	LD	A,#01
	LD	(DE),A
	INC	DE
	POP	IX
	BIT	4,(IX+#0E)
	CALL	NZ,PutDIRTxt
	CALL	Z,GetFileLen
	LD	A,#01
	LD	(DE),A
	INC	DE
	CALL	GetData
	LD	A,#01
	LD	(DE),A
	INC	DE
	CALL	GetTime
	POP	BC
	POP	DE
	POP	HL
	PUSH	DE
	PUSH	BC
	PUSH	HL
	LD	HL,WorkBuffer+#200
	LD	B,#26
	CALL	PrintName
	POP	HL
	LD	BC,#0019
	ADD	HL,BC
	POP	BC
	LD	A,C
	ADD	A,#08
	LD	C,A
	POP	DE
	LD	A,(HL)
	DEC	HL
	OR	A
	JR	Z,PrnDIRp2
	DJNZ	PrnDIRp
	JR	PrnDIRpE
PrnDIRp2
	DEC	B
	JR	Z,PrnDIRpE
	LD	HL,EmptyFile1
PrnDIRp3
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	B,#26
	CALL	PrintName
	POP	BC
	LD	A,C
	ADD	A,#08
	LD	C,A
	POP	DE
	POP	HL
	DJNZ	PrnDIRp3
PrnDIRpE
	POP	BC
	LD	A,B
	OUT	(PAGE1),A
	LD	A,C
	OUT	(YPORT),A
	POP	IY
	RET 
PutDIRTxt
	EX	AF,AF'
	LD	HL,DIRt1
	LD	A,(IX+#01)
	CP	"."
	JR	Z,$+5
	LD	HL,DIRt2
	LD	BC,#000A
	LDIR 
	EX	AF,AF'
	RET 
DIRt1	DEFB	#20,#10,"UP--DIR",#11
DIRt2	DEFB	#20,#10,"SUB-DIR",#11

EmptyFile1
	DEFB	#00,"        "
	DEFB	#01,"   "
	DEFB	#01,"          "
	DEFB	#01,"        "
	DEFB	#01,"     "
PrintDIR2
;Делево	каталогов
	IN	A,(PAGE1)
	PUSH	AF
	BIT	0,(IY+PanelFlg)
	LD	A,(DIRpage1)
	JR	Z,$+5
	LD	A,(DIRpage2)
	OUT	(PAGE1),A
	LD	HL,WorkBuffer
	LD	B,#18
	LD	A,#80
	PUSH	HL
	LD	(HL),A
	INC	L
	DJNZ	$-2
	POP	DE
	LD	HL,#4000
	LD	C,(IY+PanelFFl)
	LD	B,(IY+PanelFFh)
	LD	A,B
	OR	C
	JR	Z,PrDIRb2
PrDIRb1	PUSH	BC
	LD	A,(HL)
	AND	#7F
	LD	E,A
	LD	A,(HL)
	LD	(DE),A
	LD	BC,#0010
	ADD	HL,BC
	POP	BC
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,PrDIRb1
PrDIRb2	LD	DE,WorkBuffer+#100
	LD	BC,#12*#10
	CALL	MoveBytes
	POP	AF
	OUT	(PAGE1),A
	LD	B,PanelCol*#10+FileCol
	LD	C,PanelCol*#10+SFileCol
	CALL	InsPrnColor
	IN	A,(YPORT)	;Сохраняем Y порт
	LD	C,A
	IN	A,(PAGE1)
	LD	B,A
	PUSH	BC
	LD	A,#50
	OUT	(PAGE1),A
	BIT	0,(IY+PanelFlg)
	LD	HL,WorkBuffer+#100
	LD	DE,#000A
	JR	Z,$+5
	LD	DE,#014A
	LD	BC,#123F
PrnDIRt	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	DE,WorkBuffer+#300
	CALL	MakeTreeLine
	POP	BC
	POP	DE
	POP	HL
	PUSH	DE
	PUSH	BC
	PUSH	HL
	LD	HL,WorkBuffer+#300
	LD	B,#24
	PUSH	IY
	CALL	PrintName
	POP	IY
	POP	HL
	LD	BC,#000F
	ADD	HL,BC
	POP	BC
	LD	A,C
	ADD	A,#08
	LD	C,A
	POP	DE
	LD	A,(HL)
	INC	HL
	INC	A
	JR	Z,PrnDIRt2
	DEC	B
	JP	NZ,PrnDIRt
	JR	PrnDIRtE
PrnDIRt2
	DEC	B
	JR	Z,PrnDIRtE
	LD	HL,EmptyFile2
	PUSH	IY
PrnDIRt3
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	B,#24
	CALL	PrintName
	POP	BC
	LD	A,C
	ADD	A,#08
	LD	C,A
	POP	DE
	POP	HL
	DJNZ	PrnDIRt3
	POP	IY
PrnDIRtE
	POP	BC
	LD	A,B
	OUT	(PAGE1),A
	LD	A,C
	OUT	(YPORT),A
	RET 
MakeTreeLine
	SUB	A
	LD	(DE),A
	INC	E
	LD	C,(HL)
	INC	HL
	LD	A,(HL)
	INC	HL
	LD	LX,A
	LD	A," "
	LD	(DE),A
	INC	E
	INC	C
	DEC	C		;r.C=0 "\"
	JR	Z,MTreeL2

	PUSH	HL
	LD	A,C
	AND	#7F
	LD	B,A
	LD	HL,WorkBuffer
	PUSH	HL
	LD	L,A
	LD	(HL),C
	POP	HL
	DEC	B
	JR	Z,MTreeL1

MTreeL0	INC	HL
	BIT	7,(HL)
	LD	A,0xb3	;"ѓ"
	JR	Z,$+4
	LD	A," "
	LD	(DE),A
	INC	E
	BIT	2,(IY+ViewFlg)
	JR	NZ,$+6
	LD	A," "
	LD	(DE),A
	INC	E
	DJNZ	MTreeL0

MTreeL1	BIT	7,C
	LD	A,0xc3;"“"
	JR	Z,$+4
	LD	A,0xc0;"ђ"
	LD	(DE),A
	INC	E
	BIT	2,(IY+ViewFlg)
	JR	NZ,$+6
	LD	A,0xc4;"”"
	LD	(DE),A
	INC	E
	POP	HL

MTreeL2	LD	A,LX
	BIT	1,A
	JR	Z,MTreeL3
	BIT	2,(IY+ViewFlg)
	JR	NZ,$+3
	DEC	E
	LD	A,0xdd;"­"
	LD	(DE),A
	INC	E
	LD	A,LX
MTreeL3	BIT	0,A
	LD	A,#02
	JR	NZ,$+3
	INC	A
	LD	(DE),A
	INC	E
	SUB	A
	LDI 
	CP	(HL)
	JR	NZ,$-3
	LD	A,#03
	LD	(DE),A
	INC	E
	LD	A,LX
	BIT	1,A
	JR	Z,MTreeL5
	LD	A,0xde;"®"
	LD	(DE),A
	INC	E
MTreeL5	LD	A,#28
	SUB	E
	JR	Z,MTreeL6
	JR	C,MTreeL6
	LD	B,A
	LD	A,#20
	LD	(DE),A
	INC	E
	DJNZ	$-2
MTreeL6	LD	A,LX
	BIT	0,A
	RET	Z
	LD	E,#26
	LD	A,#02
	LD	(DE),A
	INC	E
	LD	A,#11
	LD	(DE),A
	RET 
EmptyFile2
	DEFB	#00
	DEFS	#24," "

InsPrnColor
	LD	HL,PrnINKd1	;INK буфер
	LD	DE,PrnPAPd1	;PAPER буфер
	PUSH	BC
	LD	A,B
	PUSH	AF
	AND	#0F		;INK
	LD	C,A
	RLCA 
	RLCA 
	RLCA 
	RLCA 
	OR	C
	CPL 
	LD	B,#08
	LD	(HL),A
	INC	HL
	DJNZ	$-2
	LD	B,A
	POP	AF
	AND	#F0		;PAPER
	LD	C,A
	RRCA 
	RRCA 
	RRCA 
	RRCA 
	OR	C
	XOR	B		;XOR с INK
	LD	B,#08
	LD	(DE),A
	INC	DE
	DJNZ	$-2
	POP	BC
	LD	HL,PrnINKd2	;INK буфер
	LD	DE,PrnPAPd2	;PAPER буфер
	LD	A,C
	PUSH	AF
	AND	#0F		;INK
	LD	C,A
	RLCA 
	RLCA 
	RLCA 
	RLCA 
	OR	C
	CPL 
	LD	B,#08
	LD	(HL),A
	INC	HL
	DJNZ	$-2
	LD	B,A
	POP	AF
	AND	#F0		;PAPER
	LD	C,A
	RRCA 
	RRCA 
	RRCA 
	RRCA 
	OR	C
	XOR	B		;XOR с INK
	LD	B,#08
	LD	(DE),A
	INC	DE
	DJNZ	$-2
	RET 

PrnINKd1 DEFS	 8,0
PrnPAPd1 DEFS	 8,0
PrnINKd2 DEFS	 8,0
PrnPAPd2 DEFS	 8,0

PrintName
	LD	LY,C		;Y координата
	LD	A,(HL)
	INC	HL
	PUSH	DE
	EXX 
	LD	HL,PrnPAPd1	;INK буфер
	LD	DE,PrnINKd1	;PAPER буфер
	OR	A
	JR	Z,$+8
	LD	HL,PrnPAPd2	;INK буфер
	LD	DE,PrnINKd2	;PAPER буфер
	POP	BC
	SRL	B
	RR	C
	SET	6,B
	EXX 
	DI 
	IN	A,(PAGE0)
	PUSH	AF
	LD	A,(FONTpage)
	OUT	(PAGE0),A
	LD	A,B
	LD	C,L		;Адрес строки
	LD	B,H
	LD	D,D
	LD	H,#08
	LD	B,B
PTxtLnD	EX	AF,AF'
	LD	A,(BC)
	INC	BC
	CP	#03
	JR	NZ,PTxtLD0
	EXX 
	LD	HL,PrnPAPd1	;INK буфер
	LD	DE,PrnINKd1	;PAPER буфер
	EXX 
	JR	PTxtLD1-2
PTxtLD0	CP	#02
	JR	NZ,PTxtLD1
	EXX 
	LD	HL,PrnPAPd2	;INK буфер
	LD	DE,PrnINKd2	;PAPER буфер
	EXX 
	LD	A,(BC)
	INC	BC
PTxtLD1	PUSH	BC
	CP	#01
	LD	B,#04
	JR	NZ,PTxtLD2
	LD	B,#02
	LD	A,#20
PTxtLD2	LD	L,A
	LD	H,high Font-#C0
	LD	E,(HL)
	INC	H
	LD	A,(HL)
	ADD	A,high Font-#C0
	LD	D,A
	LD	HL,#0008
	EX	DE,HL
PrnName1
	LD	L,L
	LD	A,(HL)
	LD	B,B
	EXX 
	LD	A,LY
	OUT	(YPORT),A
	LD	L,L
	OR	(HL)
	LD	B,B
	EX	DE,HL
	LD	L,L
	XOR	(HL)
	LD	A,A
	LD	(BC),A
	LD	B,B
	EX	DE,HL
	INC	BC
	EXX 
	ADD	HL,DE
	DJNZ	PrnName1
	POP	BC
	EX	AF,AF'
	DEC	A
	JR	NZ,PTxtLnD
	POP	AF
	OUT	(PAGE0),A
	EI 
	RET 
PositTAB1
CNT=#3F
	DUP     #11
	DEFW	#000A
	DEFB	CNT
CNT=CNT+8
	EDUP 
CNT=#3F
 	DUP     #11
	DEFW	#0072
	DEFB	CNT
CNT=CNT+#08
 EDUP 
CNT=#3F
	DUP     #11
	DEFW	#00DA
	DEFB	CNT
CNT=CNT+#08
	EDUP 
PositTAB2
CNT=#3F
	DUP     #11
	DEFW	#014A
	DEFB	CNT
CNT=CNT+#08
	EDUP 
CNT=#3F
	DUP     #11
	DEFW	#01B2
	DEFB	CNT
CNT=CNT+#08
	EDUP 
CNT=#3F
	DUP     #11
	DEFW	#021A
	DEFB	CNT
CNT=CNT+8
	EDUP 
;[]----------------------------------------------------------[]
SetInvert
	BIT	7,(IY+PanelFlg)
	RET	Z
	LD	H,PanelCol*#10+FileCol
	LD	L,PanelCol*#10+SFileCol
	LD	B,InvPCol*#10+InvICol
	LD	C,InvPCol*#10+InvSCol
	JR	PrnInvert
ResInvert
	BIT	7,(IY+PanelFlg)
	RET	Z
	LD	H,InvPCol*#10+InvICol
	LD	L,InvPCol*#10+InvSCol
	LD	B,PanelCol*#10+FileCol
	LD	C,PanelCol*#10+SFileCol
PrnInvert
	LD	A,(IY+PanelEFl)
	OR	(IY+PanelEFh)
	RET	Z
	BIT	1,(IY+ViewFlg)
	JP	NZ,PrnTreeInv
	PUSH	HL
	IN	A,(PAGE1)
	PUSH	AF
	BIT	0,(IY+PanelFlg)
	LD	A,(DIRpage1)
	JR	Z,$+5
	LD	A,(DIRpage2)
	OUT	(PAGE1),A
	LD	L,(IY+PanelFFl)
	LD	H,(IY+PanelFFh)
	LD	E,(IY+PanelCFl)
	LD	D,(IY+PanelCFh)
	ADD	HL,DE
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	E,L
	LD	D,H
	ADD	HL,HL
	ADD	HL,DE
	SET	6,H
	LD	E,(HL)
	POP	AF
	OUT	(PAGE1),A
	POP	HL
	LD	A,E
	OR	A
	LD	E,B
	LD	D,H
	JR	Z,$+4
	LD	E,C
	LD	D,L
	BIT	0,(IY+ViewFlg)
	JR	NZ,PrnInvertP
	LD	L,(IY+PanelCFl)
	LD	H,(IY+PanelCFh)
	LD	C,L
	LD	B,H
	ADD	HL,HL
	ADD	HL,BC
	BIT	0,(IY+PanelFlg)
	LD	BC,PositTAB1
	JR	Z,$+5
	LD	BC,PositTAB2
	ADD	HL,BC
	PUSH	DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	C,(HL)
	LD	B,#00
	EX	DE,HL
	POP	DE
	LD	LX,#30
	LD	HX,#08
	DEC	HL
	DEC	HL
	CALL	InvertRectan
	RET 
PrnInvertP
	LD	L,(IY+PanelCFl)
	LD	H,(IY+PanelCFh)
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,#003F
	ADD	HL,BC
	LD	C,L
	LD	B,#00
	BIT	0,(IY+PanelFlg)
	LD	HL,#0006
	JR	Z,$+5
	LD	HL,#0146
	LD	LX,#92
	LD	HX,#08
	CALL	InvertRectan
	RET 
PrnTreeInv
	PUSH	HL
	IN	A,(PAGE1)
	PUSH	AF
	BIT	0,(IY+PanelFlg)
	LD	A,(DIRpage1)
	JR	Z,$+5
	LD	A,(DIRpage2)
	OUT	(PAGE1),A
	LD	L,(IY+PanelFFl)
	LD	H,(IY+PanelFFh)
	LD	E,(IY+PanelCFl)
	LD	D,(IY+PanelCFh)
	ADD	HL,DE
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	SET	6,H
	LD	A,(HL)
	EX	AF,AF'
	INC	HL
	LD	A,(HL)
	LD	HX,A
	LD	LX,#FF
	INC	LX
	INC	HL
	LD	A,(HL)
	OR	A
	JR	NZ,$-5
	LD	A,LX
	ADD	A,A
	ADD	A,A
	LD	LX,A
	POP	AF
	OUT	(PAGE1),A
	POP	HL
	LD	A,HX
	OR	A
	LD	E,B
	LD	D,H
	JR	Z,$+4
	LD	E,C
	LD	D,L
	LD	L,(IY+PanelCFl)
	LD	H,(IY+PanelCFh)
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,#003F
	ADD	HL,BC
	LD	C,L
	LD	B,#00
	BIT	0,(IY+PanelFlg)
	LD	HL,#0012
	JR	Z,$+5
	LD	HL,#0152
	BIT	2,(IY+ViewFlg)
	JR	Z,PrTreeI
	EX	AF,AF'
	OR	A
	JR	Z,PrTreeI+1
	EX	AF,AF'
	LD	A,HX
	OR	A
	JR	Z,PrTreeI
	PUSH	BC
	LD	C,#08
	ADD	HL,BC
	POP	BC
PrTreeI	EX	AF,AF'
	PUSH	BC
	AND	#7F
	LD	C,A
	SLA	C
	RL	B
	SLA	C
	RL	B
	SLA	C
	RL	B
	ADD	HL,BC
	BIT	2,(IY+ViewFlg)
	JR	NZ,$+3
	ADD	HL,BC
	POP	BC
	LD	HX,#08
	CALL	InvertRectan
	RET 


