		DEVICE ZXSPECTRUM128
		include	"fn_export.inc"
		org	0x80EA
;		section	.text

EXEhead:	db	"EXE"
		db	0			; +3
		dw	EntryExec-EXEhead
		dw	0x0000			; +4
		dw	EXEend-EntryExec	; +8
		dw	0, 0			; +10
		dw	0			; +14
		dw	EntryExec		; +16
		dw	EntryExec
		dw	0x80FF
Args:		EQU	$-0x80
EntryExec:	DI
		LD	A, (IX-0x03)		; file handle
		LD	(.hFile), A		; save
		LD	HL, .copyright_str
		LD	C, 0x5C			; print text
		RST	0x10
		LD	C, 0x00
		RST	0x10
		LD	A, D
		OR	A
		JR	NZ, .next1
		LD	HL, .incor_dos_str
		LD	C, 0x5C			; print text
		RST	0x10
		LD	BC, 0xFF41
		RST	0x10			; exit
.next1
		LD	BC, 0x063D
		RST	0x10			; need 6 memory pages
		JR	NC, .next2		; CY - not enough memory
		LD	HL, .no_memory_str
		LD	C, 0x5C			; print text
		RST	0x10
		LD	BC, 0xFF41
		RST	0x10			; exit
.next2
		LD	(.hMem), A		; memory handle
		LD	HL, .Buffer
		LD	C, 0xC5
		RST	0x08			; get memory pages list
		LD	A, (.MPg)		; set pages from 0xC000
		OUT	(0xE2), A
		LD	HL, FNMain	;0xffff-(FNMainEnd-FNMain)		;FNMAIN
		LD	DE, FNMainEnd-FNMain
		LD	A, (.hFile)
		LD	C, 0x13
		RST	0x10
		JP	C, .disk_error
		; LD	HL, 0xffff-(FNMainEnd-FNMain)		;Распаковка FNMAIN
		; LD	DE, 0x8500
		; call	UnHrust

		LD	A, (.FPg)
		OUT	(0xE2), A
		LD	HL, FNFunc	;0xffff-(FNFuncEnd-FNFunc)		;FNFUNCT
		LD	DE, FNFuncEnd-FNFunc
		LD	A, (.hFile)
		LD	C, 0x13
		RST	0x10
		JP	C, .disk_error
		; LD	HL, 0xffff-(FNFuncEnd-FNFunc)		;Распаковка FNFUNCT
		; LD	DE, 0xC000
		; call	UnHrust

		LD	A, (.VPg)		;FNVIEW
		OUT	(0xE2), A
		LD	HL, FNView	;0xffff-(FNViewEnd-FNView)
		LD	DE, FNViewEnd-FNView
		LD	A, (.hFile)
		LD	C, 0x13
		RST	0x10
		JP	C, .disk_error
		; LD	HL, 0xffff-(FNViewEnd-FNView)		;Распаковка FNVIEW
		; LD	DE, 0xC000
		; call	UnHrust

		LD	A, (.EPg)		;FNEDIT
		OUT	(0xE2), A
		LD	HL, FNEdit	;0xffff-(FNEditEnd-FNEdit)
		LD	DE, FNEditEnd-FNEdit
		LD	A, (.hFile)
		LD	C, 0x13
		RST	0x10
		JP	C, .disk_error
		; LD	HL, 0xffff-(FNEditEnd-FNEdit)		;Распаковка FNEDIT
		; LD	DE, 0xC000
		; call	UnHrust

		LD	A, (.HPg)		;FNHELP
		OUT	(0xE2), A
		LD	HL, FNHelp	;0xffff-(FNHelpEnd-FNHelp)
		LD	DE, FNHelpEnd-FNHelp
		LD	A, (.hFile)
		LD	C, 0x13
		RST	0x10
		JP	C, .disk_error
		; LD	HL, 0xffff-(FNHelpEnd-FNHelp)		;Распаковка FNHELP
		; LD	DE, 0xC000
		; call	UnHrust

		LD	A, (.FNPg)		;FNFONT
		OUT	(0xE2), A
		LD	HL, FNFont	;0xffff-(FNFontEnd-FNFont)
		LD	DE, FNFontEnd-FNFont
		LD	A, (.hFile)
		LD	C, 0x13
		RST	0x10
		JP	C, .disk_error
		; LD	HL, 0xffff-(FNFontEnd-FNFont)		;Распаковка FNFONT
		; LD	DE, 0xC000
		; call	UnHrust

		LD	A, (.hFile)
		LD	C, 0x12
		RST	0x10			;close file
		LD	A, (.MPg)
		OUT	(0xE2), A
		LD	A, (.hMem)
		LD	(FNIndef), A
		LD	HL, .FPg
		LD	DE, FunctPg
		LD	BC, 0x0005
		LDIR
		JP	EntryPoint
.disk_error:
		LD	A, (.hMem)		; free memory
		LD	C, 0x3E
		RST	0x10
		LD	A, (.hFile)
		LD	C, 0x12
		RST	0x10			; close file
		LD	HL, .load_err_str
		LD	C, 0x5C			; print text
		RST	0x10
		LD	BC, 0xFF41
		RST	10h			; exit

.hFile		db	0x00
.hMem		db	0x00
.Buffer
.MPg		db	0x00
.FPg		db	0x00
.VPg		db	0x00
.EPg		db	0x00
.HPg		db	0x00
.FNPg		db	0x00
		db	0x00

.copyright_str:	db	0x0D, 0x0A
		db	"The Flex Navigator, ver."
		include "version.inc"		;Manager version
		db	0x0D, 0x0A
		db	"Original version (C) 2000, Enin Anton, St-Petersburg.", 0x0D, 0x0A
		db	"Modified version (C) 2021, Mikhaltchenkov Dmitry, Rostov-on-Don.", 0x0D, 0x0A, 0x00
.incor_dos_str:	db	"Incorrect DOS version, need DOS 1.00 or high.", 0x0D, 0x0A, 0x00
.no_memory_str:	db	"Not enough memory to load program.", 0x0D, 0x0A, 0x00
.load_err_str:	db	"Disk loading error.", 0x0D, 0x0A, 0x00

EXEend:

		org #8500
FNMain
		insert	"bin\fnmain.bin"
FNMainEnd
		org #c000
FNFunc
		insert	"bin\fnfunc.bin"
FNFuncEnd
		org #c000
FNView
		insert	"bin\fnview.bin"
FNViewEnd
		org #c000
FNEdit
		insert	"bin\fnedit.bin"
FNEditEnd
		org #c000
FNHelp
		insert	"bin\fnhelp.bin"
FNHelpEnd
		org #c000
FNFont
		insert	"bin\fnfont.bin"
FNFontEnd


		savebin	"bin\fnloader.exe",EXEhead,EXEend-EXEhead